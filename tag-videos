#! /usr/bin/perl

# Copyright (c) 2024 Marc Espie <espie@openbsd.org>
# 
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
# 
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

use v5.36;

# XXX this actually expects Gnu Readline for tab completion to work
use Term::ReadLine;
use FindBin;
use Getopt::Long qw(:config bundling require_order);
use File::Spec;
use File::Temp;
use lib "$FindBin::Bin/lib";
use TagVideos::Model;
use TagVideos::Path;
use File::Copy qw();
use File::Basename qw();

use locale;

my ($quick, $quick2, $cwd, $dbpath, $opt_L, $opt_l, $history, $ttyname, 
    @external, $smart);


if (!GetOptions(
    q => \$quick,
    Q => \$quick2,
    s => \$smart,
    'c=s' => \$cwd,
    'd=s' => \$dbpath,
    'l' => \$opt_l,
    L => \$opt_L,
    'H=s' => \$history,
    't=s' => \$ttyname,
    'X=s' => sub($X, $value) {
    	push(@external, split(/\,/, $value));
	},
    'x=s' => \@external) || @ARGV >1 ) {
	TagVideos::Base->usage("[-LlqQ]", "[-c cwd]", "[-d database]",
		"[-H history]", "[-t tty]", "[-[x|X] cmd...]", "path");
	exit 1;
}

# as it stands the script gets two parameters: cwd + path name

my $path;

if ($opt_l && -r 'video_tags') {
	$dbpath //= 'video_tags';
}
if ($quick2) {
	$quick = 2;
}

my ($in, $out) = (\*STDIN, \*STDOUT);

if (defined $ttyname) {
	open(my $tty, "+<", $ttyname) or die;
	if (!-t $tty) {
		say STDERR "$ttyname is not a tty";
		exit 1;
	}
	select($tty);
	($in, $out) = ($tty, $tty);
}

my $db = TagVideos::Model->connect($dbpath);

if (@ARGV == 1) {
	$path = File::Spec->rel2abs($ARGV[0], $cwd);

	if (!$opt_L) {
		$path = TagVideos::Path->resolve_link($path);
	}


	say "Filename is: $path";

	$db->set_path($path);
	my $descr = $db->read_descr;
	if (defined $descr) {
		say "Descr:";
		say $descr;
	}

}

my $term = Term::ReadLine->new("tag-videos", $in, $out);
my $attribs = $term->Attribs;
$attribs->{completion_entry_function} = $attribs->{list_completion_function};

$history //= $ENV{HOME}."/.tag-videos-history";

if (-f $history) {
	$term->ReadHistory($history);
}

sub list_tags($quit = 0)
{
	my $tags = $db->find_tags;

	say "Tags: ", join(' ', @$tags);
	if ($quit && (@$tags > 0 || $quit == 2)) {
		exit 0;
	}
}

sub prepare_completions()
{
	$attribs->{completion_word} = 
		$db->selectcol_arrayref('alltags');
}

sub may_alter_tag($tag, $h)
{
	if ($h->{$tag}) {
		say "$tag renamed to $h->{$tag}";
		return $h->{$tag};
	}
	return $tag;
}

sub parse_tags($words, $f)
{
	my $h = $db->renames;
	for my $tag (split /\s+/, lc($words)) {
		if ($smart) {
			$tag = may_alter_tag($tag, $h);
		}
		&$f($tag);
	}
}

sub pretty_print_suggestions($h)
{
	my @l;
	for my $k (sort {
		$h->{$a} != $h->{$b} ?
		$h->{$b} <=> $h->{$a} : $a cmp $b } keys %$h) {
		push(@l, "$k($h->{$k})");
	}
	return @l;
}

sub lazy_levenshtein_load()
{
	state $loaded = 0;
	state $tried = 0;
	if (!$tried) {
		eval {
		    require Text::LevenshteinXS;
		};
		if ($@) {
			say STDERR "You must install Text::LevenshteinXS for this function";
			$tried = 1;
		} else {
			$loaded = 1;
		}
	}
	$tried = 1;
	return $loaded;
}

sub levenshtein_sort($max, $amortized, @tags)
{
	return unless lazy_levenshtein_load();
	@tags = sort @tags;
	my $h = {};
	while (@tags > 1) {
		my $word = shift @tags;
		for my $w (@tags) {
			my $d = Text::LevenshteinXS::distance($word, $w);
			if ($amortized) {
				$d = $d / log(length($word)+ length($w));
			}
			$h->{join('/', $word, $w)} = $d;
		}
	}
	my $i = 0;
	my $maxd;
	my @values;
	for my $k (sort {$h->{$a} <=> $h->{$b}} keys %$h) {
		my $d = $h->{$k};
		if (defined $maxd && $d > $maxd) {
			say join(', ', sort @values), ": ", $maxd;
			$i += @values;
			@values = ();
			return if $i > $max;
		}
		$maxd = $d;
		push(@values, $k);
    	}
}

sub levenshtein_closest($word)
{
	return $word unless lazy_levenshtein_load();
	my @tags = @{$attribs->{completion_word}};
	my $dmin;
	my $min;
	for my $w (@tags) {
		my $d = Text::LevenshteinXS::distance($word, $w);
		if ($d > 0 && (defined $dmin && $d < $dmin || !defined $dmin)) {
			$dmin = $d;
			$min = $w;
			last if $d == 1;
		}
	}
	return "$word($min)";
}

sub levenshtein_debug($max)
{
	my @r = $db->occurrences($max);
	my $i = 0;
	for my $e (@r) {
		next if !defined $e;
		say $i, ":", join(' ', map {levenshtein_closest($_)} @$e);
		$i++;
	}
}

sub edit_description()
{
	my $fh = File::Temp->new(
		UNLINK => 0, 
		TEMPLATE => "/tmp/edit.XXXXXXXXXXX"
	    );
	my $tempname = $fh->filename;
	my $descr = $db->read_descr;
	if (defined $descr) {
		print $fh $descr;
	}
	close $fh;
	my $editor = $ENV{VISUAL} // $ENV{EDIT} // "vi";
	system("$editor $tempname");
	open(my $fh2, "<", $tempname) or die;
	local $/;
	undef $/;
	$descr = <$fh2>;
	close $fh2;
	$db->set_descr($descr);
	unlink($tempname);
}


if (defined $path) {
	list_tags($quick);
}

if (@external) {
	push(@external, $0);
	if (defined $cwd) {
		push(@external, '-c', $cwd);
	}
	if (defined $opt_L) {
		push(@external, '-L');
	}
	push(@external, '-H', $history);
	if (defined $ttyname) {
		push(@external, '-t', $ttyname);
	}
	push(@external, @ARGV);
	exec {$external[0]} @external;
}

prepare_completions();

while (defined ($_ = $term->readline('> '))) {
	if (m/^\!?tag\s.*\sIF\s.*/) {
		$db->parse_rule($_);
	} elsif (m/^\!?rename\b/) {
		$db->parse_rule($_);
		prepare_completions();
	} elsif (m/^(?:add|t|tag)\b\s*(.*)/i) {
		parse_tags($1, 
		    sub($tag) {
			$db->create_tag($tag);
		    });
		prepare_completions();
	} elsif (m/^rm\b\s*(.*)/i) {
		parse_tags($1, 
		    sub($tag) {
			$db->delete_tag($tag);
		    });
	} elsif (m/^suggest\b\s*(.*)/i) {
		parse_tags($1,
		    sub($tag) {
			my $tags = $db->suggestions($tag);
			say "Suggestions: ", 
			    join(' ', pretty_print_suggestions($tags));
		    });
	} elsif (m/^list\s*$/i) {
		list_tags();
	} elsif (m/^move\b\s+(.*)$/i) {
		my $newname = $1;
		if (File::Copy::move($path, $newname)) {
			if (-d $newname) {
				$newname = File::Spec->catfile($newname,
				    File::Basename::basename($path));
			}
			$newname = File::Spec->rel2abs($newname, $cwd);
			if (!$opt_L) {
				$newname = TagVideos::Path->resolve_link($path);
			}
		}
	} elsif (m/^known\b\s*(.*)$/) {
		my $filter = $1;
		my @tags = @{$attribs->{completion_word}};
		if ($filter ne '') {
			@tags = grep { /\Q$filter\E/ } @tags;
		}
		say "known tags: ", join(' ', @tags);
	} elsif (m/^distance\b\s*(\d+)?$/i) {
		my $max = $1 // 25;
		my @tags = @{$attribs->{completion_word}};
		levenshtein_sort($max, 0, @tags);
	} elsif (m/^distancelog\b\s*(\d+)?$/i) {
		my $max = $1 // 25;
		my @tags = @{$attribs->{completion_word}};
		levenshtein_sort($max, 1, @tags);
	} elsif (m/^debug\b\s*(\d+)?$/i) {
		my $max = $1 // 3;
		levenshtein_debug($max);
	} elsif (m/^help\s*|^\?\s+$/i) {
		say "help/man/add/t/rm/rename/list";
		say "suggest/distance/distancelog...";
		say "showrules/rmrules/descr/known/wipe/quit"
	} elsif (m/^(?:quit|q)\s*$/i) {
		$db->cleanup;
		$term->WriteHistory($history);
		exit;
	} elsif (m/^descr\s*/i) {
		edit_description();
	} elsif (m/^showrules\s*(.*)/i) {
		$db->show_rules($1);
	} elsif (m/^rmrule\s+(.*)$/i) {
		$db->delete_rule($1);
	} elsif (m/^wipe\s*$/i) {
		$db->wipe_tags;
	} elsif (m/^man\b/) {
		system {'man'} (qw(man tag-videos));
	} elsif (m/^\s*$/) {
		# nothing !
	} else {
		say "Unknown command $_";
	}
	prepare_completions();
}
